plugins {
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'org.springframework.boot' version '2.3.4.RELEASE'
    id 'com.palantir.docker' version '0.22.1'
}

apply from: file('setupTestsReports.gradle')

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'com.palantir.docker'

    group = 'preference-center'
    version = '0.0.1-SNAPSHOT'

    ext {
        set('springCloudVersion', "Hoxton.SR8")
    }

    compileJava {
        sourceCompatibility = 11
        targetCompatibility = 11
    }

    bootJar {
        archiveFileName = "${archiveBaseName.get()}-${version}.${archiveExtension.get()}"
    }

    task unpack(type: Copy) {
        dependsOn bootJar
        from(zipTree(tasks.bootJar.outputs.files.singleFile))
        into("build/dependency")
    }

    docker {
        name "samiraghayarov/${bootJar.archiveBaseName.get()}"
        copySpec.from(tasks.unpack.outputs).into("dependency")
        buildArgs(['DEPENDENCY': "dependency"])
        tag 'DockerHub', "samiraghayarov/${bootJar.archiveBaseName.get()}"
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    test {
        useJUnitPlatform()
    }

}